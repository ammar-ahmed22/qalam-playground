// Helper function to create an array of random integers
kaam random_arr(length, mini, maxi) {
  rakho res = [];
  har(rakho i = 0; i < length; i++) {
    push(res, random_int(mini, maxi));
  }
  wapis res;
} 

rakho length = 10;
rakho mini = -20;
rakho maxi = 20;
rakho arr = random_arr(length, mini, maxi);
bolo("Random array:");
bolo(arr);

kaam merge(left, right) {
  rakho l = 0;
  rakho r = 0;
  rakho res = [];

  jabtak(l < len(left) && r < len(right)) {
    agar(left[l] < right[r]) {
      push(res, left[l]);
      l++;
    } warna {
      push(res, right[r]);
      r++;
    }
  }

  jabtak(l < len(left)) {
    push(res, left[l]);
    l++;
  }

  jabtak(r < len(right)) {
    push(res, right[r]);
    r++;
  }

  wapis res;
}

kaam merge_sort(arr) {
  agar (len(arr) <= 1) {
    wapis arr;
  }

  rakho m = floor(len(arr) / 2);
  rakho left = [];
  rakho right = [];
  har(rakho i = 0; i < len(arr); i++) {
    agar(i < m) {
      push(left, arr[i]);
    } warna {
      push(right, arr[i]);
    }
  }

  rakho sorted_left = merge_sort(left);
  rakho sorted_right = merge_sort(right);
  wapis merge(sorted_left, sorted_right);
}

rakho sorted = merge_sort(arr);
bolo("Sorted array:");
bolo(sorted);
