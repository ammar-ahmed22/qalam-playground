// Showcasing classes and inheritance with a LibraryItem model
// Books and movies's are both LibraryItem's so we can use inheritance

// Defining the LibraryItem class
jamat LibraryItem {
  // Class constructor
  banao(title, year) {
    // yeh is equivalent to `this` or `self`
    yeh.title = title;
    yeh.year = year;
  }

  get_info() {
    wapis "Title: " + yeh.title + ", Year: " + str(yeh.year);
  }
}

// Defining a Book
jamat Book shamil LibraryItem {
  banao(title, year, author, pages) {
    // asli is equivalent to `super`
    asli.banao(title, year);
    yeh.author = author;
    yeh.pages = pages;
  }

  // Overriding
  get_info() {
    wapis asli.get_info() + ", Author: " + yeh.author + ", Pages: " + str(yeh.pages);
  }
}

// Defining a Movie
jamat Movie shamil LibraryItem {
  banao(title, year, director, run_time) {
    asli.banao(title, year);
    yeh.director = director;
    yeh.run_time = run_time;
  }

  // Overriding
  get_info() {
    wapis asli.get_info() + ", Director: " + yeh.director + ", Run time: " + str(yeh.run_time) + " hours";
  } 
}

// Making some library items
rakho items = [];
push(items, Book(
  "Quran",
  609,
  "Allah",
  604
));
push(items, Book(
  "Sahih Bukhari",
  846,
  "Muhammad Al-Bukhari",
  4000
));
push(items, Movie(
  "The Message",
  1976,
  "Moustapha Akkad",
  2 + (57 / 60)
));

// Getting info on all items
har(rakho i = 0; i < len(items); i++) {
  bolo(items[i].get_info());
}
