// Declaring a variable
// Every statement must end with a semi-colon
shai name = "Ammar";

// Printing to the console
qul("Hello " + name); 

// Number type
shai a = 3;
shai b = 1.4;
qul(a + b);

// String type
shai last_name = "Ahmed";
shai full_name = name + " " + last_name;
qul(full_name);

// Boolean type
shai is_true = haqq;
shai is_false = batil;

// If-else statements
itha (is_true) {
  qul("it is true!"); // only this will run
} illa {
  qul("it is false!");
}

// Null type
shai null = ghaib;
shai uninitialized; // uninitialized variables are `ghaib` by default
qul(null);
qul(uninitialized);

// Arrays
// Arrays can have any type
shai arr = [1, 1.1, "Ammar", haqq, ghaib];

// Indexed assignment
arr[0] = 3;

// Built-in function for push
push(arr, "I was pushed on");

// Iterating over arrays with a for loop
tawaf(shai i = 0; i < len(arr); i++) {
  qul("at index " + str(i) + ": " + str(arr[i]));
}

// Iterating with while loop
shai i = 0;
baynama(i < len(arr)) {
  qul("in while loop, at index " + str(i) + ": " + str(arr[i]));
  i++;
}

// Function declaration
amal calc_sum(arr) {
  shai tot = 0;
  tawaf(shai i = 0; i < len(arr); i++) {
    tot += arr[i];
  }
  radd tot;
}

shai arr = [1, 2, 3, 4, 5, 6];
qul("Sum of array: " + str(calc_sum(arr)));