// Declaring a variable
// Every statement must end with a semi-colon
rakho name = "Ammar";

// Printing to the console
bolo("Hello " + name); 

// Number type
rakho a = 3;
rakho b = 1.4;
bolo(a + b);

// String type
rakho last_name = "Ahmed";
rakho full_name = name + " " + last_name;
bolo(full_name);

// Boolean type
rakho is_true = sach;
rakho is_false = jhoot;

// If-else statements
agar (is_true) {
  bolo("it is true!"); // only this will run
} warna {
  bolo("it is false!");
}

// Null type
rakho null = khali;
rakho uninitialized; // uninitialized variables are `khali` by default
bolo(null);
bolo(uninitialized);

// Arrays
// Arrays can have any type
rakho arr = [1, 1.1, "Ammar", sach, khali];

// Indexed assignment
arr[0] = 3;

// Built-in function for push
push(arr, "I was pushed on");

// Iterating over arrays with a for loop
har(rakho i = 0; i < len(arr); i++) {
  bolo("at index " + str(i) + ": " + str(arr[i]));
}

// Iterating with while loop
rakho i = 0;
jabtak(i < len(arr)) {
  bolo("in while loop, at index " + str(i) + ": " + str(arr[i]));
  i++;
}

// Function declaration
kaam calc_sum(arr) {
  rakho tot = 0;
  har(rakho i = 0; i < len(arr); i++) {
    tot += arr[i];
  }
  wapis tot;
}

rakho arr = [1, 2, 3, 4, 5, 6];
bolo("Sum of array: " + str(calc_sum(arr)));
